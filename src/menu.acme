; the main menu page, i.e. the front-page / directory of the magazine

; theme colours
MENU_BORDER     = BLACK
MENU_FORE       = BLACK
MENU_BACK       = LTGREY

;===============================================================================
; table of interrupts to use for the menu screen:
;
menu_irqs:
       +irqs_table_item menu_irq_bottom,        SCANLINE_VBLANK_BEGIN + 1
       +irqs_table_item menu_irq_top,           SCANLINE_VBLANK_END + (3 * 8)

menu_irq_top:
        ;-----------------------------------------------------------------------
        lda # MENU_BACK
        sta VIC_BACKGROUND
        rts

menu_irq_bottom:
        ;-----------------------------------------------------------------------
        lda # MENU_BORDER
        sta VIC_BACKGROUND
        rts

;===============================================================================
menu_screen:
        ; setup the screen:
        ;-----------------------------------------------------------------------
        ; turn the screen off whilst we clear it
        lda # %00001000
        sta VIC_SCREEN_CTL1
        
        ; set our own interrupt handlers
        ; A = address of IRQ table, lo-byte
        ; X = address of IRQ table, hi-byte
        ; Y = number of entries in the table (1-based)
        ;
        lda #< menu_irqs
        ldx #> menu_irqs
        ldy # 2
        jsr irqs_load

        ; set screen colour
        lda # MENU_BORDER
        sta VIC_BORDER
        lda # MENU_BACK
        sta VIC_BACKGROUND

        ; erase the text-screen
        lda # MENU_FORE
        jsr clear_screen

        ; print logo:
        ;-----------------------------------------------------------------------
        ; print the logo with the base colour;
        ; we're going to recolour just the letters
        lda # DKGREY
        sta ZP_COLOR
       +print_str_pos       petscii_logo

        ; begin at the end of the logo and work backwards
        ; (easier for loop management to count toward 0)
        ldy # (3 * 40)
        ldx # BLUE      ; colour of letters

-       dey
        bmi +           ; finished?

        lda $0400, y    ; read a character from the logo
        cmp # $64       ; is the character a background-line?
        beq -           ; yes, skip it and don't change the colour
        txa
        sta $d800, y
        bpl -           ; (always branches)

+       lda # BLACK
        sta ZP_COLOR
        
        ldx # 40
        lda # $63
-       dex
        sta $0400 + (3 * 40), x
        bne -

        ;-----------------------------------------------------------------------
        ; turn screen on
        lda # %00011011
        sta VIC_SCREEN_CTL1

PAGE_X  = 1
PAGE_Y  = 4

       +print_str_pos   subline

       +print_str_at    PAGE_Y + 2, PAGE_X,   thorne
       +print_str_pos   item01

        lda # DKGREY
        sta ZP_COLOR
       +print_str_pos   item02
       +print_str_pos   item03
       
        jmp *
        rts

petscii_logo:
;===============================================================================
        !byte   0, 0
!ct "build/petscii_font.ct" {
        !text   " ---- =  = ---- ---- ----- ---- ----    "
        !text   " #  # #  # #    #  # # # # #___ #       "
        !text   " #  # #__# #___ #__# # # # #___ #       ", 0
}
subline:
        !byte   PAGE_Y, PAGE_X + 30
        !scr    "issue #1", 0

thorne:
        !scr    $db, 0
item01:
        !byte   PAGE_Y + 2, PAGE_X + 2
        !scr    "Editorial", 0
item02:
        !byte   PAGE_Y + 4, PAGE_X + 2
        !scr    "Why 8-bit?", 0
item03:
        !byte   PAGE_Y + 6, PAGE_X + 2
        !scr    "The outfit", 0