; the main menu page, i.e. the front-page / directory of the magazine

; theme colours
MENU_BORDER     = BLACK
MENU_FORE       = BLACK
MENU_BACK       = LTGREY

; currently selected menu item
!addr   ZP_MENU_INDEX           = $10
; number of menu entries
!addr   ZP_MENU_COUNT           = $11
; address of the menu entry offsets, lo-bytes
!addr   ZP_MENU_DB_OFFL         = $12
!addr   ZP_MENU_DB_OFFL_LO      = $12
!addr   ZP_MENU_DB_OFFL_HI      = $13
; address of the menu entry offsets, hi-bytes
!addr   ZP_MENU_DB_OFFH         = $14
!addr   ZP_MENU_DB_OFFH_LO      = $14
!addr   ZP_MENU_DB_OFFH_HI      = $15
; address of the menu entry rows
!addr   ZP_MENU_DB_ROWS         = $16
!addr   ZP_MENU_DB_ROWS_LO      = $16
!addr   ZP_MENU_DB_ROWS_HI      = $17
; address of the menu entry columns
!addr   ZP_MENU_DB_COLS         = $18
!addr   ZP_MENU_DB_COLS_LO      = $18
!addr   ZP_MENU_DB_COLS_HI      = $19

;===============================================================================
menu_screen:
        ; setup the screen:
        ;-----------------------------------------------------------------------
        ; turn the screen off whilst we clear it
        lda # %00001000
        sta VIC_SCREEN_CTL1
        
        ; set our own interrupt handlers
        ; A = address of IRQ table, lo-byte
        ; X = address of IRQ table, hi-byte
        ; Y = number of entries in the table (1-based)
        ;
        lda #< menu_irqs
        ldx #> menu_irqs
        ldy # 4
        jsr irqs_load

        ; set screen colour
        lda # MENU_BORDER
        sta VIC_BORDER
        lda # MENU_BACK
        sta VIC_BACKGROUND

        ; erase the text-screen
        lda # MENU_FORE
        jsr clear_screen

        ; print logo:
        ;-----------------------------------------------------------------------
        ; print the logo with the base colour;
        ; we're going to recolour just the letters
        lda # DKGREY
        sta ZP_PRINT_COLOR
       +print_str_pos       menu_petscii_logo

        ; begin at the end of the logo and work backwards
        ; (easier for loop management to count toward 0)
        ldy # (3 * 40)
        ldx # BLUE      ; colour of letters

-       dey
        bmi +           ; finished?

        lda $0400, y    ; read a character from the logo
        cmp # $64       ; is the character a background-line?
        beq -           ; yes, skip it and don't change the colour
        txa
        sta $d800, y
        bpl -           ; (always branches)

+       lda # BLACK
        sta ZP_PRINT_COLOR
        
        ldx # 40
        lda # $63
-       dex
        sta $0400 + (3 * 40), x
        bne -

       +print_str_pos   menu_subline

        ; print menu items:
        ;-----------------------------------------------------------------------
        ; select the first menu item by default
        lda # 0
        sta ZP_MENU_INDEX
        
        ; draw the menu items on the screen
        jsr menu_refresh

        ;-----------------------------------------------------------------------
        ; turn screen on
        lda # %00011011
        sta VIC_SCREEN_CTL1
       
        jmp *

        ; tear down the custom interrupt handlers
        jsr disable_interrupts
        rts

menu_refresh:
;===============================================================================
        ; the first byte of the menu database is the number of menu items.
        ; we also render the menu items from last to first for speed purposes
        ldy menu_db_count
        sty ZP_MENU_COUNT
        ; the menu-count is 1-based;
        ; subtract 1 to avoid printing garbage
        dey

        lda # DKGREY
        sta ZP_PRINT_COLOR
        
        ; position the cursor:
-       lda menu_db_cols, y     ; get the column
        tax                     ; in X
        sty @i+1                ; (backup index)
        lda menu_db_rows, y     ; get the row
        tay                     ; in Y
        jsr print_set_crsr      ; set cursor: Y = row, X = col

        ; print the string:
@i      ldy # $88               ; retrieve the menu index
        lda menu_db_strlo, y    ; load the string address lo-byte into X
        tax
        lda menu_db_strhi, y    ; load the string address hi-byte into A
        jsr print_str           ; print the string!
        dey
        bpl -

        ; get the screen address of the selected menu item:
        ldy ZP_MENU_INDEX
        lda menu_db_cols, y     ; column number
        tax                     ; must be in X
        lda menu_db_rows, y     ; row in Y
        jsr print_get_crsr      ; returns X = addr lo-byte, A = addr hi-byte
        stx @scr+1
        sta @scr+2
        lda # $db
@scr    sta $8888

        rts

menu_petscii_logo:
;===============================================================================
        !byte   0, 0
!ct "build/petscii_font.ct" {
        !text   " ---- =  = ---- ---- ----- ---- ----    "
        !text   " #  # #  # #    #  # # # # #___ #       "
        !text   " #  # #__# #___ #__# # # # #___ #       ", 0
}
menu_subline:
        !byte   4, 31
        !scr    "issue #0", 0

menu_db:
;===============================================================================
!source "build/menu_db.acme"

;===============================================================================
; table of interrupts to use for the menu screen:
;
menu_irqs:
       +irqs_table_item menu_irq_vblank,        SCANLINE_VBLANK_BEGIN
       +irqs_table_item menu_irq_logo_end,      SCANLINE_VBLANK_END + (3 * 8)-1
       +irqs_table_item menu_irq_logo,          SCANLINE_VBLANK_END + (2 * 8)-1
       +irqs_table_item menu_irq_logo,          SCANLINE_VBLANK_END + (1 * 8)-1

menu_irq_logo:
        ;-----------------------------------------------------------------------
        lda # DKGREY
        sta VIC_BORDER

        ; now kill enough time for a whole line
        ldx # 11
-       dex
        bne -

        ; go back to black
        stx VIC_BORDER

        rts

menu_irq_logo_end:
        ;-----------------------------------------------------------------------
        lda # DKGREY
        sta VIC_BORDER
        lda # LTGREY
        sta VIC_BACKGROUND

        ; now kill enough time for a whole line
        ldx # 10
-       dex
        bne -
        ; go back to black
        stx VIC_BORDER

        ; now kill enough time for a whole line
        ldx # 3
-       dex
        bne -

        lda # LTGREY
        sta VIC_BORDER

        rts

menu_irq_vblank:
        ;-----------------------------------------------------------------------
        lda # MENU_BORDER
        sta VIC_BORDER
        sta VIC_BACKGROUND
        rts