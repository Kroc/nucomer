; nÃ¼comer magazine (c) copyright Kroc Camen 2019-2020. unless otherwise noted,
; licenced under Creative Commons Attribution Non-Commercial Share-Alike 4.0
; licence; you may reuse and modify this code how you please as long as you:
;
; - retain the copyright notice
; - use the same licence for your derived code
; - do not use it for commercial purposes
;   (contact the author for a commercial licence)
;
;===============================================================================
!source "nucomer.acme"          ; nucomer constants for memory layout

!source "build/intro_symbols.acme"
!sl     "build/nucomer_symbols.acme"

* = nu_main

;===============================================================================
; NOTE: exomizer includes BSOD64 in the payload, so we don't need to
;       manually load it from the outfit. TODO: load an exomized BSOD64
;       file using the IRQ fast-loader during some off-time (menu-screen?)
;
init:   ; setup screen:
        ;-----------------------------------------------------------------------
        lda # %00110111
        sta .CPU_CONTROL
        
        ; turn the screen off whilst we fiddle with the VIC settings
        lda # %00000000
        sta .VIC_SCREEN_CTL1
        ; clear the garbage byte!
        sta vic_garbage+0

        ; change the VIC bank
        ;
        ldx .CIA2_PORTA_DDR     ; (backup current value)
        lda # %00000111         ; we only want to write to the low 3-bits
        sta .CIA2_PORTA_DDR     ; set the port's read/write state
        ;
        ; the bit value is the inverse of the bank number 0-3
        lda # (! NU_VIC_BANK) & %11
        sta .CIA2_PORTA         ; change VIC bank

        ; we must restore the read/write state
        ; of the port or disk I/O will break!
        stx .CIA2_PORTA_DDR

        ; change the VIC memory layout to position the text-screen & font
        lda # ((>nu_screen & %00111100) << 2) | ((>nu_font & %00111000) >> 2)
        sta .VIC_LAYOUT

        ; start menu screen:
        ;-----------------------------------------------------------------------
        jsr irqs_init

        ; turn off BASIC & KERNAL ROM,
        ; leave I/O on (BSOD64 will be under there)
        ;;dec .CPU_CONTROL
        dec .CPU_CONTROL
        dec .CPU_CONTROL

        ; from here, there is no going back...
        jsr menu_screen
        
        ; halt and catch fire
        jmp *

;===============================================================================

!source "print.acme"            ; global string-printing routines
!source "keyboard.acme"

!source "menu.acme"
!source "read.acme"


!ifdef WARN_OVERFLOW !eof
!if * >= nu_gfx {
        WARN_OVERFLOW = 1
        !warn "Outfit overflows its boundary! * = ", *
}