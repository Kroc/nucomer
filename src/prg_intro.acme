; nÃ¼comer magazine (c) copyright Kroc Camen 2019. unless otherwise noted,
; licenced under Creative Commons Attribution Non-Commercial Share-Alike 4.0
; licence; you may reuse and modify this code how you please as long as you:
;
; - retain the copyright notice
; - use the same licence for your derived code
; - do not use it for commercial purposes
;   (contact the author for a commercial licence)
;
;===============================================================================
!source "c64/c64.acme"
!source "config.acme"           ; nucomer constants for memory layout

* = nu_intro

; TODO: fast-load the outfit, whilst the intro plays
;
intro:
        ;-----------------------------------------------------------------------
        ; setup the screen:
        ; turn the screen off whilst we clear it
        lda # %00000000
        sta VIC_SCREEN_CTL1

        ; the intro runs in VIC bank 2 ($8000-$BFFF), specifically so that we
        ; can load the main outfit into $0400+ without garabge appearing on the
        ; screen. we don't use VIC bank 3, as the outfit does, because that
        ; would require loading a font
        ;
        ldx CIA2_PORTA_DDR      ; (backup current value)
        lda # %00000111         ; we only want to write to the low 3-bits
        sta CIA2_PORTA_DDR      ; set the port's read/write state
        ;
        ; the bit value is the inverse of the bank number 0-3
        lda # (! 2) & %111
        sta CIA2_PORTA          ; change VIC bank

        ; we must restore the read/write state of the port
        ; or disk I/O will break!
        stx CIA2_PORTA_DDR

        ; change the VIC memory layout to position the text-screen & font
        lda # ((>nu_intro_screen & %00111100) << 2) | ((>$9800 & %00111000) >> 2)
        sta VIC_LAYOUT

        ;-----------------------------------------------------------------------
        jsr logo_screen
        
        ; load the main outfit
        ;-----------------------------------------------------------------------
        ; TODO: replace this with a fast-loader
        ;
        ldx ZP_KERNAL_DEV       ; last-used KERNAL device number
        bne +                   ; not 0? good
        ldx # DEV_DRV8          ; default to drive 8
+       ldy # $01               ; use PRG load-address
        tya                     ; logical file number
        jsr KERNAL_SETLFS

        ldx #< @filename
        ldy #> @filename
        lda # 7                 ; A is string-length
        jsr KERNAL_SETNAM       ; set KERNAL file name

        lda # 0                 ; load, not verify
        ;;ldx #< nu_main
        ;;ldy #> nu_main
        jsr KERNAL_LOAD
        bcc +
        
        jam

        ; jump to the main outfit
+       jmp nu_main + 13

@filename:
        ;-----------------------------------------------------------------------
        !pet    "nucomer"

;===============================================================================
!source "logo.acme"