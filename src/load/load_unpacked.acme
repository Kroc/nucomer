; COVERT BITOPS Autoconfiguring Loader/Depacker V2.28
; with 1541/1571/1581/CMD FD/CMD HD/IDE64/Fastdrive-emu autodetection & support
;===============================================================================
;
; LOADFILE
;
; Loads an unpacked file
;
; Parameters: X (low),Y (high): Address of null-terminated filename
; Returns: C=0 OK, C=1 error (A holds errorcode)
; Modifies: A,X,Y
;
loadfile:
        ;-----------------------------------------------------------------------
        jsr openfile
        jsr getbyte             ; get startaddress lowbyte
        bcs @fail               ; if EOF at first byte, error (file not found)
        sta .zp_destlo
        jsr getbyte             ; get startaddress highbyte
        sta .zp_desthi

        ldy # $00
@loop:  jsr getbyte
        bcs @eof

        !if LOAD_UNDER_IO > 0 {
                ;///////////////////////////////////////////////////////////////
                jsr disableio   ; allow loading under I/O area
        }       ;///////////////////////////////////////////////////////////////
        sta (.zp_destlo),y

        !if LOAD_UNDER_IO > 0 {
                ;///////////////////////////////////////////////////////////////
                jsr enableio
        }       ;///////////////////////////////////////////////////////////////
        iny
        bne @loop
        inc .zp_desthi
        jmp @loop

@eof:   cmp # $01               ; returncode 0 = OK, others error
@fail:  rts